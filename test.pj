def map = f(arr, fn) {
	def iter = f(arr, acc) {
		if(len(arr) == 0)  return acc;

		return iter(rest(arr), push(acc, fn(first(arr))));
	}
	return iter(arr, []);
}

def filter = f(arr, fn) {
	def iter = f(arr, acc) {
		if(len(arr) == 0)  return acc;
		if(fn(first(arr))) 
			return iter(rest(arr), push(acc, first(arr)));

		return iter(rest(arr), acc);
	}
	return iter(arr, []);
}

def reduce = f(arr, init, fn) {
	def iter = f(arr, result) {
		if(len(arr) == 0) 
			return result;
		
		return iter(rest(arr), fn(result, first(arr)));
	}

	return iter(arr, init);
}

def sum = f(arr) => reduce(arr, 0, f(x, y) => x + y);
def foo = 6;
def myArray = [1,2,3,4, 4 + 1, foo]
def squared = map(myArray, f(x) => x ** 2);
def filtered = filter(myArray, f(x) => x % 2 == 0);

print("original:", myArray);
print("squared :", squared);
print("sum     :", sum(myArray));
print("filtered:", filtered);
